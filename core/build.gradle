plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            includes.from("README.md")
            noAndroidSdkLink.set(false)
        }
    }
}

dependencies {
    //dokkaHtmlPlugin 'org.jetbrains.dokka:kotlin-as-java-plugin:1.5.30'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'androidx.paging:paging-runtime-ktx:3.0.1'
    api 'androidx.room:room-runtime:2.3.0'
    api 'androidx.room:room-ktx:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    api 'androidx.preference:preference-ktx:1.1.1'
    api 'androidx.annotation:annotation:1.3.0-rc01'
    api 'com.google.code.gson:gson:2.8.6'
    api 'com.dhh:gson-ktx:1.0.3'
    api 'de.prosiebensat1digital.oasis-jsbridge-android:oasis-jsbridge-quickjs:0.14.2'
}

task dokkaJavadocJar(type: Jar) {
    dependsOn(tasks.dokkaJavadoc)
    def directories = new ArrayList<Object>()
    tasks.dokkaJavadoc.each {
        directories.add(it.outputDirectory)
    }
    from(directories)
    archiveClassifier.set("javadoc")
}

task dokkaHtmlJar(type: Jar) {
    dependsOn(tasks.dokkaHtml)
    def directories = new ArrayList<Object>()
    tasks.dokkaHtml.each {
        directories.add(it.outputDirectory)
    }
    from(directories)
    archiveClassifier.set("html-doc")
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives dokkaJavadocJar
    archives dokkaHtmlJar
}

group = 'knf.hydra'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'knf.hydra'
                artifactId = 'core'
                version = '1.0-rc1'
                artifact(androidSourcesJar)
                artifact(dokkaJavadocJar)
                artifact(dokkaHtmlJar)
            }
        }
    }
}